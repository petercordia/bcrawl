##############################################################################
# forest.des: Maps for the Forest.
# - entry vaults
# - minivaults
# - branch end vaults
##############################################################################

##############################################################################
# Forest entry vaults
##############################################################################

##############################################################################
NAME:   dummy_forest_entry
WEIGHT: 50
TAGS:   forest_entry
KFEAT:  O = enter_forest
MAP
O
ENDMAP

##############################################################################
# A really basic entry.
NAME:   grunt_forest_entry_basic
TAGS:   forest_entry vaults_entry_forest
KFEAT:  O = enter_forest
MONS:   plant / nothing
MAP
.........
..t...t..
.t11.11t.
..1t.t1..
....O....
..1t.t1..
.t11.11t.
..t...t..
.........
ENDMAP

##############################################################################
# A small clearing.
NAME:   grunt_forest_entry_clearing
TAGS:   forest_entry vaults_entry_forest
KFEAT:  O = enter_forest
MONS:   spriggan rider
MONS:   satyr / spriggan druid
MONS:   apis, black bear
SUBST:  t = t.
KMONS:  P = plant / bush
NSUBST: Q = 3:1 / 1:2
NSUBST: R = 4:3 / 1:4 / 1:5 / *:.
MAP
.tt...tt.
t..Q.Q..t
t.t.Q.t.t
.Q.P.P.Q.
..Q.O.Q..
.Q.P.P.Q.
t.t.Q.t.t
t..Q.Q..t
.tt...tt.
ENDMAP

##############################################################################
# A secluded grove.
NAME:   forest_entry_grove
TAGS:   forest_entry vaults_entry_forest vaults_orient_s
KFEAT:  O = enter_forest
MONS:   spriggan rider
MONS:   satyr / spriggan druid
MONS:   apis, black bear
KMONS:  P = plant / bush
NSUBST: Q = 3:1 / 1:2
NSUBST: R = 4:3 / 1:4 / 1:5 / *:.
MAP
   xxxxxxxxx
  xxxttttxxxx
 xxxttttttttxx
xxtttt...tttxxx
xttt..Q.Q..ttxx
xttt.Q.R.Q.tttx
xtt.........ttx
xtt.R..O..R.ttx
xtt..R.R.R..ttx
xttt..R.R..tttx
xttt.......tttx
xxtttt...ttttxx
 xxxtt..ttxxxx
  xxxtt..ttxx
   xxtt..ttx
ENDMAP

NAME:    bh_autumn_forest_stairs
# No threat or loot, so allow another vault.
TAGS:    extra
ORIENT:  float
DEPTH:   Forest
COLOUR:  t = lightred / red w:3
SHUFFLE: Aaq / Bbr / Ccs
SUBST:   q = @
SUBST:   A = ., bBcCdDrs = t
COLOUR:  t = lightred / red w:3 / green w:3 / lightgreen w:1
TILE:    t = dngn_tree_lightred / dngn_tree_red w:4 / dngn_tree_yellow w:4
COLOUR:  a = lightred / red / green / lightgreen
TILE:    a = dngn_tree_lightred / dngn_tree_red / dngn_tree_yellow / dngn_tree
SUBST:   a = t
MAP
  aaaa qqq aaaa
 caaaaaAAAaaaaab
cccaaaaAAAaaaabbb
ccc...........bbb
ccc...........bbb
 cc..xxxxxxx..bb
sCC..x.....x..BBr
sCC..x..{..x..BBr
sCC..x.....x..BBr
 cc..xV...Vx..bb
ccc..xx+++xx..bbb
ccc...........bbb
ccc...........bbb
ccctttttttttttbbb
 cctttttttttttbb
  ctttt    tttb
ENDMAP

##############################################################################
# Forest minivaults
##############################################################################
NAME: grunt_forest_small_clearing_fruit
TAGS: transparent extra
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
FTILE: . = floor_grass / floor_dirt
KMONS: O = plant / bush
KITEM: . = ration / w:90 nothing
MAP
 tt...tt
t.......t
t.......t
.........
.........
.........
t.......t
t.......t
 tt...tt
ENDMAP

NAME: grunt_forest_small_clearing_pond
TAGS: transparent extra
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
FTILE: . = floor_grass / floor_dirt
SUBST: w = wwW
SUBST: W = WW.
KMONS: O = plant / bush
MAP
 tt...tt
t.......t
t..WWW..t
..WwwwW..
..WwwwW..
..WwwwW..
t..WWW..t
t.......t
 tt...tt
ENDMAP

NAME: grunt_forest_small_clearing_treed
TAGS: transparent patrolling
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
FTILE: . = floor_grass / floor_dirt
KMONS: O = plant / bush
MONS: spriggan rider
MAP
 tt...tt
t.......t
t.1...1.t
...ttt...
...t*t...
...ttt...
t.1...1.t
t.......t
 tt...tt
ENDMAP

NAME: grunt_forest_small_clearing_statue_fruit
TAGS: transparent
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
FTILE: . = floor_grass / floor_dirt
KMONS: O = plant / bush
KITEM: . = ration / w:90 nothing
MAP
 tt...tt
t.......t
t.......t
.........
....G....
.........
t.......t
t.......t
 tt...tt
ENDMAP

NAME: grunt_forest_small_clearing_statues
TAGS: transparent
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
FTILE: . = floor_grass / floor_dirt
KMONS: O = plant / bush
MONS: faun, faun / satyr w:3
NSUBST: 7 = 3:1 / 1:2
MAP
 tt...tt
t.......t
t.G.7.G.t
.........
..7.*.7..
.........
t.G.7.G.t
t.......t
 tt...tt
ENDMAP

NAME: grunt_forest_large_clearing_fruit
TAGS: transparent extra
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
FTILE: . = floor_grass / floor_dirt
KMONS: O = plant / bush
KITEM: . = ration / w:90 nothing
MAP
      tt...tt
    ttt.....ttt
  ttt.........ttt
  t.............t
 tt.............tt
 t...............t
tt...............tt
t.................t
...................
...................
...................
t.................t
tt...............tt
 t...............t
 tt.............tt
  t.............t
  ttt.........ttt
    ttt.....ttt
      tt...tt
ENDMAP

NAME: grunt_forest_large_clearing_guards
TAGS: transparent patrolling
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
KMONS: O = plant / bush
MONS: spriggan rider, spriggan air mage, spriggan defender / spriggan berserker
NSUBST: P = 4:1 / 3:2 / 1:3
MAP
      tt...tt
    ttt.....ttt
  ttt.........ttt
  t.............t
 tt.............tt
 t...............t
tt...............tt
t.......P.P.......t
.......P%.%P.......
.........G.........
.......P*.%P.......
t.......P.P.......t
tt...............tt
 t...............t
 tt.............tt
  t.............t
  ttt.........ttt
    ttt.....ttt
      tt...tt
ENDMAP

NAME:  grunt_forest_large_clearing_statues
TAGS:  transparent
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
KMONS: O = plant / bush
KMONS: * = satyr / spriggan berserker
KITEM: . = ration / w:90 nothing
MAP
      tt...tt
    ttt.....ttt
  ttt.........ttt
  t.............t
 tt.............tt
 t.......G.......t
tt....G.....G....tt
t.................t
...................
.....G...*...G.....
...................
t.................t
tt....G.....G....tt
 t.......G.......t
 tt.............tt
  t.............t
  ttt.........ttt
    ttt.....ttt
      tt...tt
ENDMAP

NAME:  grunt_forest_large_clearing_slash
TAGS:  transparent extra
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
SUBST: w = wwW
SUBST: W = WW.
KMONS: O = plant / bush
KITEM: . = ration / w:90 nothing
MAP
      tt...tt
    ttt.....ttt
  ttt.........ttt
  tww...........t
 ttwww..........tt
 t..www..........t
tt...www.........tt
t.....www.........t
.......wwW.........
........WWW........
.........Www.......
t.........www.....t
tt.........www...tt
 t..........www..t
 tt..........wwwtt
  t...........wwt
  ttt.........ttt
    ttt.....ttt
      tt...tt
ENDMAP

NAME:  grunt_forest_large_clearing_hut
TAGS:  transparent
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
KMONS: O = plant / bush
COLOUR: -*c = lightgrey
FTILE:  -* = floor_normal
MAP
      tt...tt
    ttt.....ttt
  ttt.........ttt
  t.............t
 tt.............tt
 t...............t
tt...............tt
t......ccccc......t
.......c--*c.......
.......+-9*c.......
.......c--*c.......
t......ccccc......t
tt...............tt
 t...............t
 tt.............tt
  t.............t
  ttt.........ttt
    ttt.....ttt
      tt...tt
ENDMAP

# These vaults are derived from spriggan_forest.
NAME: kb_forest_berserker_hut_1
DEPTH: Forest
MONS:  spriggan berserker
KMONS: P = plant / plant / bush / nothing
SUBST: T = t / t / P / .
MAP
   tttttt
ttttPP.Tt
tccccT..@
tc%1c...T
tc..+..Tt
tc.1c...T
tcccct.Tt
tttT..ttt
 tTPTTt
 tTTttt
 tttt
ENDMAP

NAME: kb_forest_berserker_hut_2
DEPTH: Forest
MONS:  spriggan berserker
KMONS: P = plant / plant / bush / nothing
SUBST: T = t / t / P / .
KFEAT: _ = altar_trog
MAP
tt@t
t..tttttttt
t....xxxxxt
tT...x..1xt
tT...+...xt
ttt.tx..1xt
  tt.x.1_xt
   ttxxxxxt
    ttttttt
ENDMAP

NAME: kb_forest_witch_hut
DEPTH: Forest
MONS:  greenwood witch
SUBST: T = t / .
MAP
tt@tttt
t.ttttt
t.xxxxt
t.+.1xt
tTx.%xt
ttcccct
ttttttt
ENDMAP

NAME:  kb_forest_spriggan_shepherd
DEPTH: Forest
SUBST: T = t / t / P / .
COLOUR: "MN = green
KMONS: P = plant
MONS:  apis, spriggan ; robe . quarterstaff
ITEM:  ration q:3
MAP
ttttttt@t
txxxtt.tttttttt
txd+"""""""tt.@
txxx""""""""".tt
ttt""2"""""""TTt
ttT""11"11"TTTtt
tt""""1""""Tttt
tt".11""2TTtt
tt""1""""Ttt
ttt""""TTTt
  ttttttttt
ENDMAP

NAME:  kb_forest_druid_statue
DEPTH: Forest
MONS:  spriggan druid
MAP
tt@tt
t.1.t
t1G1t
t.1.t
ttttt
ENDMAP

# XXX: this one doesn't work very well outside the corner, or as a primary
# vault...
NAME:   kb_forest_skeletal_bear_cave
DEPTH:  Forest
ORIENT: southeast
MONS:   greenwood witch
MONS:   black bear skeleton
MAP
                   tttttt
                ttttxxxxx
            t@tttxxxxxxxx
            tt..xxxxxxxxx
          tttt....xxxxxxx
        tttt......}xxxxxx
       tt.....xxxxxxxxxxx
ttt  ttt....xxxxxxxxxxxxx
@.tttt...xxxxxxxxsxxxsxxx
tt.......xxxxx..sxxxxssxx
 tttxxxxx......xx.sxx..xx
txxxxxxxxxx....xxx.1.xxxx
 txxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
# Forest ends
##############################################################################
NAME:   grunt_forest_end_chambers
ORIENT: north
PLACE:  Forest:$
KITEM:  O = superb_item
KITEM:  1 = superb_item
KMONS:  O = the Enchantress / Agnes / spriggan defender, spriggan air mage
KMONS:  1 = spriggan defender
MONS:   spriggan defender, spriggan berserker, spriggan druid
MONS:   spriggan rider, spriggan air mage
MONS:   spriggan defender / spriggan berserker / spriggan druid / \
        spriggan rider / spriggan air mage / nothing w:100
MONS:   thorn hunter / shambling mangrove
NSUBST: $ = 30:* / 6:% / *:$
NSUBST: * = 14:| / *:*
TILE:   c = wall_marble
COLOUR: .|*%OT123456+ = lightgray
FTILE:  .|*%OT123456+ = floor_normal
MAP
       tttttttttttttttttt         
    tttttttttccccccccttttttttt    
   ttttttttccc||||||ccctttttttt   
   tttttttcc**********ccttttttt   
   ttttttcc$...4444...$cctttttt   
   tttttcc$............$ccttttt   
   ttttcc$..6.5....5.6..$cctttt   
   ttttc$$.6..........6.$$ctttt   
   ttttc$$.6..........6.$$ctttt   
   ttttc$$.6....O1....6.$$ctttt   
   ttttc$$.6....11....6.$$ctttt   
   ttttc$$.6..........6.$$ctttt   
   ttttc$$.6..5....5..6.$$ctttt   
   ttttcc$..6........6..$cctttt   
   tttttcc..............ccttttt   
  tttttttcc....4224....ccttttttt  
 tttcccccccc..........ccccccccttt 
tttcc......ccc......ccc......ccttt
ttcc.4.4.4..cccc++cccc..4.4.4.cctt
tcc..........c.6..6.c..........cct
tc$..........c+.66.+c..........$ct
tc$$.....1....cccccc....1.....$$ct
tc$$$..T.2....cccccc....2.T..$$$ct
tc$$..........cccccc..........$$ct
tc$.6........c+.66.+c........6.$ct
tcc.6........c.6..6.c........6.cct
ttc..66.....cccc++cccc.....66..ctt
ttccc..66..cccc....cccc..66..ccctt
ttttccc..cccc........cccc..cccLttt
tLttLLcccccc...6446...ccccccLtLtLt
ttLLLLtLLLcc..........cctLLLtttLLt
ttLLLLLLLLc..6......6..cLLtLLtttLt
tLLtLLL7LLc..6..TT..6..cLLLLtLLLLt
ttLLLLLLLLc..6..TT..6..cLLLLLLLLLt
tttLtLLLLLc..6......6..cLLtLLLtLLt
ttLLLLLtLLcc..........ccLLL7LLLLLt
ttLL7LLLLLLc...6666...cLLLLLtLL7Lt
tLLLLLLLLLLcc........ccLLtLLLLLLtt
ttLLtLL7LLLLccc....cccLL7LLLtLtttt
tttttLLLLLLLLLc++++cLLLLLLtttttttt
tttttttLLLLLLLLLLLLLLLLttLLLtttttt
tttttttttttLLLLLLLLLLtttLLLLLttttt
tttttttttttttttLLLLtttLLLttttttttt
 tttttttttttttttLLLLLLLtttttttttt 
  ttttttttttttttL@LLtttttttttttt  
ENDMAP

NAME:   kb_forest_enchantress_palace
ORIENT: float
PLACE:  Forest:$
MONS:   spriggan rider / spriggan druid / \
        spriggan berserker / spriggan defender / \
        spriggan air mage
MONS:   spriggan rider
MONS:   spriggan druid
MONS:   spriggan berserker
MONS:   spriggan air mage
MONS:   spriggan defender
KMONS:  O = the Enchantress / Agnes / Jorgrun / spriggan air mage, spriggan air mage
KMONS:  A = greenwood witch
KMONS:  B = will-o'-wisp
KMONS:  X = spriggan rider / spriggan druid / \
            spriggan berserker / spriggan defender / \
            spriggan air mage
KMONS:  Y = spriggan air mage
KFEAT:  ~ = +
SUBST:  . = .:100 / 1:2 / 2:1 / 3:1 / 4:1 / 5:1
SUBST:  T = t / t / P / .
KMONS:  P = plant / plant / bush / nothing
KMONS:  R = plant
KMONS:  Q = bush
KMONS:  K = hornet
KITEM:  f = dagger / short sword / rapier / flail / whip /  shortbow
KITEM:  g = robe / cloak / hat / buckler
KMONS:  L = spriggan ; robe . dagger
KITEM:  h = ration q:2
COLOUR: C = white
KFEAT:  C = stone_wall
TILE:   C = wall_marble
NSUBST: - = 8:| / 20:* / 8:% / 15:$ / *:-
KFEAT:  - = floor
KFEAT:  _ = floor
FTILE:  R-_~67ABXYfg$*| = floor_normal
MAP
 tttttttttttttttttt     ttttttttttttttt    
 ttCCCCCCCCCCCCCCtttt ttttttttttttttttttt  
 ttC-----O------CtttttttttTTTTTttTTTTttttt 
tttC--B------A--CtttQTtttTT.T.Ttt.T.TTTttt 
tttC----6__6----Ctt..ccccc....cccc.TT.Tttt 
tttCCCCCC~~CCCCCC....c..1c....c.1c.t.TTttt 
tttC||-C6__6C-||CtT..c.1.+....+..cT.TT.ttt 
tttC||-~____~-||Ctt..ccccc....cccc.TT.Tttt 
 ttC||-C6__6C-||CttQ..................ttttt
 ttCCCCCC~~CCCCCCCCttccccc....cccc......ttt
 ttC---C-_X-CfffffCttchxLc....c1.c.....tttt
 ttC---~____~_____Cttch..+....+.4c..1...ttt
 ttC---CX__XCgggggCttccccc....cccc...1.tttt
 ttCCCCCC~~CCCCCCCCttt...................tt
 ttCR---__X_---RCccccccccc....5.........ttt
 ttC--X-____---XCcKxKxKxKc......2......tttt
 ttCRX--Y__Y--XRCc.22.3..c.....22......tttt
tttCCCCCC~~CCCCCCc+ccccccc....2.2.....tttt 
ttt...PP....PP.......................tttt  
@...................................ttttt  
t..TTttttt............5..........ttt.ttttt 
tttttttttttttttt...............tttttt...ttt
  ttttttttttttttttt.........tttttttttttt<tt
      ttttttttttttttttt....tttttttttttttt  
           ttttttttttt@tt..@ttttttttttt    
ENDMAP

NAME:   grunt_forest_end_winery
TAGS:   no_pool_fixup
PLACE:  Forest:$
ORIENT: float
KMONS:  O = the Enchantress band, Frances, spriggan defender
MONS:   spriggan, spriggan berserker / spriggan defender
MONS:   spriggan druid / spriggan air mage, plant
MONS:   faun w:20 / satyr / thorn hunter w:5
KMONS:  S = spark wasp
KMONS:  K = caustic shrike
NSUBST: 6 = 1:K / 4:S / *:d
KMONS:  7 = water nymph
KFEAT:  7 = deep_water
NSUBST: w = 2:7 / *:w
KFEAT:  T = tree / floor
SUBST:  X = xt
CLEAR:  x
KFEAT:  : = floor
ITEM:   ration q:1 / ration q:2 / nothing w:180
ITEM:   any potion / ration q:1 w:20 / nothing w:20
NSUBST: $ = 12:* / 10:% / 20:$ / *:-
NSUBST: * = 12:| / *:*
TILE:   c = wall_vines
FTILE:  e123+|%*$- = floor_vines
MAP
  tttttttttttttttttttttttttttttttttttttttt  
 tttttttttttttttttttttttttttttttttttttttttt 
XttTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTtt
XttT.5...................................Ttt
XttT..4d4d4.cccccccccccccccccccccccccccc.Ttt
XttT..4d4d4.c$$$$$$$+$$$$$$$ceeeeeec|||c.Ttt
XttT..4d4d4.c$$$$$$$c$$$$$$$c------c|||c.Ttt
XttT..4d4d4.c$$$2$$$c$$$2$$$c------cc+cc.Ttt
XttT..4d4d4.cc+ccccccc+cccccc------c***c.Ttt
XttT..4d4d45c-----3---3-----+----O-c***c.Ttt
XttT..4d4d4.c-------2-------+------c***c.Ttt
XtT...4d4d4.c--wwwwwwwwwww--+------+***c.Ttt
XtT...4d4d4.c--wwwwwwwwwww--c------c***c.Ttt
XtT...4d4d4.c-----3---3-----c------c***c.Ttt
XtT...4d4d4.c---------------ceeeeeec***c.Ttt
XTT...4d4d4.cc++cccccccccccccccccccccccc.Ttt
T.......5......5............5............Ttt
@TT...44444444..444444444444..4444444444.Ttt
TtT...ddd6dd6d..ddd6ddd6dddd..ddd6ddd6dd.Ttt
XtT...444444445.444444444444.54444444444.Ttt
XtT...d6dddd6d..d6ddd6dd6ddd..d6dddd6ddd.Ttt
XttT..44444444.54444444444445.4444444444.Ttt
XttT..dd6ddd6d..dddd6dddd6dd..d6ddd6dddd.Ttt
XttT..44444444..444444444444..4444444444.Ttt
XttT.....................................Ttt
XttT.....................................Ttt
XttTTTTTT...........TTTT...........TTTTTTTtt
XttttttttTTTTT.TTTTTTTTTTTTTT.TTTTTTTTTTTTtt
 XtttttttttttT.TttttttttttttT.Ttttttttttttt 
  XXXXXXXXXXT@TXXXXXXXXXXXXXXT@TXXXXXXXXXX  
ENDMAP

NAME:   grunt_forest_end_shrine
PLACE:  Forest:$
ORIENT: float
KMONS:  O = the Enchantress / Bai Suzhen, spriggan air mage
MONS:   spriggan, wendigo / shambling mangrove
MONS:   spriggan air mage / spriggan druid / \
        spriggan berserker / spriggan rider w:5 / \
        spriggan defender w:5 / \ 
        thorn hunter w:5 / shambling mangrove w:5
MONS:   will-o'-wisp
KFEAT:  _ = altar_xom
KFEAT:  - = floor
SUBST:  T = tx
CLEAR:  x
NSUBST: $ = 26:* / 4:% / 20:$ / *:.
NSUBST: * = 12:| / *:*
FTILE:  14|*$-+ = floor_dirt
TILE:   c = wall_church
MAP
   TTTTTTTTTTTTT
 TTTtttttttttttTT
TTttt$$$$$$c..ttTT
Ttt$3$$$$$$c..3ttT
Ttt$$cccc+cccc..tTTTTTTTT
Tt$$$c||c4***c..ttttttttt
Tt$$$c||c.***c.2t..3....t
Tt$$$cc+c-2-2+....G.G.G..
Tt$2$+-2--O_-+..2.......@
Tt$$$cc+c-2-2+....G.G.G..
Tt$$$c||c.***c.2t..3....t
Tt$$$c||c4***c..ttttttttt
Tt$$$cccc+cccc..tTTTTTTTT
Ttt$3$$$$$$c..3ttT
TTttt$$$$$$c..ttTT
 TTTtttttttttttTT
   TTTTTTTTTTTTT
ENDMAP

## A camper cannibalizes his companions to survive and turns into a wendigo
NAME:  manman_forest_wendigo_camp
DEPTH: Forest
MONS:  wendigo
: dgn.delayed_decay(_G, 'd', 'human skeleton ')
MARKER: L = lua:fog_machine { cloud_type = "flame", \
             pow_min = 1000, pow_max = 1000, delay = 1, \
             size = 1, walk_dist = 0, start_clouds = 1, excl_rad = 0 }
MAP
        tttttt
      tttttttttt
    tttttt.dtttttt
  ttttttt....ttttttt
 tttttttd.....ttttttt
ttttttt...LL1..tttttt
ttttttt...LLd..tttttt
 ttttttt......ttttt
  tttttttd..ttttt
    tttttt.tttttt
      tttt.ttttt
       ttt.ttt
        ttdtt
        tt@tt
ENDMAP

###############################################################################
## A palace where denizens of the forest gather to worship a powerful
## water deity
NAME:     gammafunk_forest_water_palace
PLACE:    Forest:$
ORIENT:   north
TAGS:     no_monster_gen no_rotate patrolling
SHUFFLE:  HI / JK, QR / OP
SUBVAULT: H : gammafunk_forest_water_palace_column
SUBVAULT: I : gammafunk_forest_water_palace_column
SUBVAULT: J : gammafunk_forest_water_palace_pool
SUBVAULT: K : gammafunk_forest_water_palace_pool
SUBVAULT: Q : gammafunk_forest_water_palace_column
SUBVAULT: R : gammafunk_forest_water_palace_column
SUBVAULT: O : gammafunk_forest_water_palace_pool
SUBVAULT: P : gammafunk_forest_water_palace_pool
MONS:     nothing w:30 / spriggan air mage / spriggan druid 
MONS:     w:20 faun / satyr, spriggan berserker
KMONS:    4 = caustic shrike
KMONS:    5 = spriggan defender
KMONS:    6 = shambling mangrove / thorn hunter
KMONS:    7 = black bear
KMONS:    8 = the Enchantress / Agnes / spriggan air mage, spriggan air mage
KMONS:    A = water elemental
KMONS:    L = greenwood witch / wendigo w:30
KMONS:    N = water nymph
KMONS:    S = elemental wellspring
KFEAT:    ALNS = shallow_water
NSUBST:   * = 10:$ / *:*
NSUBST:   $ = 4:% / *:$
COLOUR:   c = white
FTILE:    |*123456GTU+. = floor_pebble_cyan
CLEAR:    E
MAP
 ttttttttttttttttttttttttttttttttttttttttttttt 
ttt....WWWWWWWcccccccccccccccccccWWWWWWW....ttt
tt.....WWWWWWcc****|||||||||****ccWWWWWW.....tt
tt....WWWWWWcc****|||||||||||****ccWWWWWW....tt
tt....WWWGLWc*****....585....*****cWLGWWW....tt
tt....WWWWWWc****.4..........4****cWWWWWW....tt
tt....WWWWWccccccccccc+++cccccccccccWWWWW....tt
tt....WWWWccWWWWWWWWWWWWWWWWWWWWWWWccWWWW....tt
tt.1..WWWWcWWWWWWWWWWWWWWWWWWWWWWWWWcWWWW..1.tt
tt....WWLWcWWHHHWWSWWW.U.WWWSWWIIIWWcWWLW....tt
tt....WWWWcWWHHHWWWWWU...UWWWWWIIIWWcWWWW....tt
tt....WWWWcWWHHHWWWW.......WWWWIIIWWcWWWW....tt
tt.1..WWWWcWWWWWWWWW..555..WWWWWWWWWcWWWW..1.tt
tt....WWWWcWWWWWWWWW.......WWWWWWWWWcWWWW....tt
tt....WWWWcWWWQQQWWWW.....WWWWRRRWWWcWWWW....tt
tt....WWWWcWWWQQQWWWWW...WWWWWRRRWWWcWWWW....tt
tt.1..WWLWcWWWQQQWWLWWWWWWWLWWRRRWWWcWWLW..1.tt
tt....WWWWcWWWWWWWWWWWWWWWWWWWWWWWWWcWWWW....tt
tt....WWWWcWWWWWWWWNWWWWWWWNWWWWWWWWcWWWW....tt
tt....WWWWcWWWWWWWWWWWWWWWWWWWWWWWWWcWWWW....tt
tt.1..WWWWcWWWcccWWWWW...WWWWWcccWWWcWWWW..1.tt
tt....WWWWcWWWcTcWWWW.2.3.WWWWcTcWWWcWWWW....tt
tt....WWWWcWWW.7.WWWW.....WWWW.7.WWWcWWWW....tt
tt....WWWWcWWWWWWWWWW.3.2.WWWWWWWWWWcWWWW....tt
tt....WWLWcWWWWLWWWWW.....WWWWWLWWWWcWWLW....tt
tt.1..WWWWccWWWWWWWWW.....WWWWWWWWWccWWWW..1.tt
tt....WWWWWccccccccccc+++cccccccccccWWWWW....tt
tt....WWWWWWWWc$$$...........$$$cWWWWWWWW....tt
tt....WWWWWWWWc$.T...2...3...T.$cWWWWWWWW....tt
tt....WWcccWWWc$.....3...2.....$cWWWcccWW....tt
tt....WWcGcWWWc...T..2...3..T...cWWWcGcWW....tt
tt....WWWWWWWWcc...............ccWWWWWWWW....tt
tt.....WWWWNWWWccccccc+++cccccccWWWNWWWW.....tt
ttt......WWWWWWWAWWAWWWWWWAWWAWWWWWWWW......ttt
tttt......WWWWWWWWWWWWWWWWWWWWWWWWW........tttt
tttttt............T.........T............tttttt
 ttttttt.....6...................6.....ttttttt 
   ttttttt........T.........T........ttttttt   
     tttttttt.....................tttttttt     
        ttttttttt.............ttttttttt        
           ttttttttt.......ttttttttt           
           EEEEEEEEEEEE.@.EEEEEEEEEE           
ENDMAP

NAME: gammafunk_forest_column
TAGS: gammafunk_forest_water_palace_column unrand no_rotate no_flip no_vmirror \
      allow_dup patrolling no_monster_gen
KMONS:  4 = black bear
COLOUR: c = white
FTILE:  G4. = floor_pebble_cyan
MAP
ccc
cGc
.4.
ENDMAP

NAME:  gammafunk_forest_pool
TAGS:  gammafunk_forest_water_palace_pool unrand no_rotate no_vmirror
TAGS:  allow_dup patrolling no_monster_gen
KMONS: N = water nymph / will-o'-wisp
KFEAT: NL = shallow_water
MAP
WWW
WNW
WWW
ENDMAP
