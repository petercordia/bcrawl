##############################################################################
# forest.des: Maps for the Forest.
# - entry vaults
# - minivaults
# - branch end vaults
##############################################################################

##############################################################################
# Forest entry vaults
##############################################################################

##############################################################################
NAME:   dummy_forest_entry
WEIGHT: 50
TAGS:   forest_entry
KFEAT:  O = enter_forest
MAP
O
ENDMAP

##############################################################################
# A really basic entry.
NAME:   grunt_forest_entry_basic
TAGS:   forest_entry vaults_entry_forest
KFEAT:  O = enter_forest
MONS:   plant / nothing
MAP
.........
..t...t..
.t11.11t.
..1t.t1..
....O....
..1t.t1..
.t11.11t.
..t...t..
.........
ENDMAP

##############################################################################
# A small clearing.
NAME:   grunt_forest_entry_clearing
TAGS:   forest_entry vaults_entry_forest
KFEAT:  O = enter_forest
MONS:   spriggan rider
MONS:   satyr / spriggan druid
MONS:   apis, black bear
SUBST:  t = t.
KMONS:  P = plant / bush
NSUBST: Q = 3:1 / 1:2
NSUBST: R = 4:3 / 1:4 / 1:5 / *:.
MAP
.tt...tt.
t..Q.Q..t
t.t.Q.t.t
.Q.P.P.Q.
..Q.O.Q..
.Q.P.P.Q.
t.t.Q.t.t
t..Q.Q..t
.tt...tt.
ENDMAP

##############################################################################
# A secluded grove.
NAME:   forest_entry_grove
TAGS:   forest_entry vaults_entry_forest vaults_orient_s
KFEAT:  O = enter_forest
MONS:   spriggan rider
MONS:   satyr / spriggan druid
MONS:   apis, black bear
KMONS:  P = plant / bush
NSUBST: Q = 3:1 / 1:2
NSUBST: R = 4:3 / 1:4 / 1:5 / *:.
MAP
   xxxxxxxxx
  xxxttttxxxx
 xxxttttttttxx
xxtttt...tttxxx
xttt..Q.Q..ttxx
xttt.Q.R.Q.tttx
xtt.........ttx
xtt.R..O..R.ttx
xtt..R.R.R..ttx
xttt..R.R..tttx
xttt.......tttx
xxtttt...ttttxx
 xxxtt..ttxxxx
  xxxtt..ttxx
   xxtt..ttx
ENDMAP

NAME:    bh_autumn_forest_stairs
# No threat or loot, so allow another vault.
TAGS:    extra
ORIENT:  float
DEPTH:   Forest
COLOUR:  t = lightred / red w:3
SHUFFLE: Aaq / Bbr / Ccs
SUBST:   q = @
SUBST:   A = ., bBcCdDrs = t
COLOUR:  t = lightred / red w:3 / green w:3 / lightgreen w:1
TILE:    t = dngn_tree_lightred / dngn_tree_red w:4 / dngn_tree_yellow w:4
COLOUR:  a = lightred / red / green / lightgreen
TILE:    a = dngn_tree_lightred / dngn_tree_red / dngn_tree_yellow / dngn_tree
SUBST:   a = t
MAP
  aaaa qqq aaaa
 caaaaaAAAaaaaab
cccaaaaAAAaaaabbb
ccc...........bbb
ccc...........bbb
 cc..xxxxxxx..bb
sCC..x.....x..BBr
sCC..x..{..x..BBr
sCC..x.....x..BBr
 cc..xV...Vx..bb
ccc..xx+++xx..bbb
ccc...........bbb
ccc...........bbb
ccctttttttttttbbb
 cctttttttttttbb
  ctttt    tttb
ENDMAP

##############################################################################
# Forest minivaults
##############################################################################
NAME: grunt_forest_small_clearing_fruit
TAGS: transparent extra
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
FTILE: . = floor_grass / floor_dirt
KMONS: O = plant / bush
KITEM: . = ration / w:90 nothing
MAP
 tt...tt
t.......t
t.......t
.........
.........
.........
t.......t
t.......t
 tt...tt
ENDMAP

NAME: grunt_forest_small_clearing_pond
TAGS: transparent extra
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
FTILE: . = floor_grass / floor_dirt
SUBST: w = wwW
SUBST: W = WW.
KMONS: O = plant / bush
MAP
 tt...tt
t.......t
t..WWW..t
..WwwwW..
..WwwwW..
..WwwwW..
t..WWW..t
t.......t
 tt...tt
ENDMAP

NAME: grunt_forest_small_clearing_treed
TAGS: transparent patrolling
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
FTILE: . = floor_grass / floor_dirt
KMONS: O = plant / bush
MONS: spriggan rider
MAP
 tt...tt
t.......t
t.1...1.t
...ttt...
...t*t...
...ttt...
t.1...1.t
t.......t
 tt...tt
ENDMAP

NAME: grunt_forest_small_clearing_statue_fruit
TAGS: transparent
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
FTILE: . = floor_grass / floor_dirt
KMONS: O = plant / bush
KITEM: . = ration / w:90 nothing
MAP
 tt...tt
t.......t
t.......t
.........
....G....
.........
t.......t
t.......t
 tt...tt
ENDMAP

NAME: grunt_forest_small_clearing_statues
TAGS: transparent
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
FTILE: . = floor_grass / floor_dirt
KMONS: O = plant / bush
MONS: faun, faun / satyr w:3
NSUBST: 7 = 3:1 / 1:2
MAP
 tt...tt
t.......t
t.G.7.G.t
.........
..7.*.7..
.........
t.G.7.G.t
t.......t
 tt...tt
ENDMAP

NAME: grunt_forest_large_clearing_fruit
TAGS: transparent extra
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
FTILE: . = floor_grass / floor_dirt
KMONS: O = plant / bush
KITEM: . = ration / w:90 nothing
MAP
      tt...tt
    ttt.....ttt
  ttt.........ttt
  t.............t
 tt.............tt
 t...............t
tt...............tt
t.................t
...................
...................
...................
t.................t
tt...............tt
 t...............t
 tt.............tt
  t.............t
  ttt.........ttt
    ttt.....ttt
      tt...tt
ENDMAP

NAME: grunt_forest_large_clearing_guards
TAGS: transparent patrolling
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
KMONS: O = plant / bush
MONS: spriggan rider, spriggan air mage, spriggan defender / spriggan berserker
NSUBST: P = 4:1 / 3:2 / 1:3
MAP
      tt...tt
    ttt.....ttt
  ttt.........ttt
  t.............t
 tt.............tt
 t...............t
tt...............tt
t.......P.P.......t
.......P%.%P.......
.........G.........
.......P*.%P.......
t.......P.P.......t
tt...............tt
 t...............t
 tt.............tt
  t.............t
  ttt.........ttt
    ttt.....ttt
      tt...tt
ENDMAP

NAME:  grunt_forest_large_clearing_statues
TAGS:  transparent
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
KMONS: O = plant / bush
KMONS: * = satyr / spriggan berserker
KITEM: . = ration / w:90 nothing
MAP
      tt...tt
    ttt.....ttt
  ttt.........ttt
  t.............t
 tt.............tt
 t.......G.......t
tt....G.....G....tt
t.................t
...................
.....G...*...G.....
...................
t.................t
tt....G.....G....tt
 t.......G.......t
 tt.............tt
  t.............t
  ttt.........ttt
    ttt.....ttt
      tt...tt
ENDMAP

NAME:  grunt_forest_large_clearing_slash
TAGS:  transparent extra
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
SUBST: w = wwW
SUBST: W = WW.
KMONS: O = plant / bush
KITEM: . = ration / w:90 nothing
MAP
      tt...tt
    ttt.....ttt
  ttt.........ttt
  tww...........t
 ttwww..........tt
 t..www..........t
tt...www.........tt
t.....www.........t
.......wwW.........
........WWW........
.........Www.......
t.........www.....t
tt.........www...tt
 t..........www..t
 tt..........wwwtt
  t...........wwt
  ttt.........ttt
    ttt.....ttt
      tt...tt
ENDMAP

NAME:  grunt_forest_large_clearing_hut
TAGS:  transparent
DEPTH: Forest
SUBST: t = t..
SUBST: . = O.........
KMONS: O = plant / bush
COLOUR: -*c = lightgrey
FTILE:  -* = floor_normal
MAP
      tt...tt
    ttt.....ttt
  ttt.........ttt
  t.............t
 tt.............tt
 t...............t
tt...............tt
t......ccccc......t
.......c--*c.......
.......+-9*c.......
.......c--*c.......
t......ccccc......t
tt...............tt
 t...............t
 tt.............tt
  t.............t
  ttt.........ttt
    ttt.....ttt
      tt...tt
ENDMAP

# These vaults are derived from spriggan_forest.
NAME: kb_forest_berserker_hut_1
DEPTH: Forest
MONS:  spriggan berserker
KMONS: P = plant / plant / bush / nothing
SUBST: T = t / t / P / .
MAP
   tttttt
ttttPP.Tt
tccccT..@
tc%1c...T
tc..+..Tt
tc.1c...T
tcccct.Tt
tttT..ttt
 tTPTTt
 tTTttt
 tttt
ENDMAP

NAME: kb_forest_berserker_hut_2
DEPTH: Forest
MONS:  spriggan berserker
KMONS: P = plant / plant / bush / nothing
SUBST: T = t / t / P / .
KFEAT: _ = altar_trog
MAP
tt@t
t..tttttttt
t....xxxxxt
tT...x..1xt
tT...+...xt
ttt.tx..1xt
  tt.x.1_xt
   ttxxxxxt
    ttttttt
ENDMAP

NAME: kb_forest_witch_hut
DEPTH: Forest
MONS:  greenwood witch
SUBST: T = t / .
MAP
tt@tttt
t.ttttt
t.xxxxt
t.+.1xt
tTx.%xt
ttcccct
ttttttt
ENDMAP

NAME:  kb_forest_spriggan_shepherd
DEPTH: Forest
SUBST: T = t / t / P / .
COLOUR: "MN = green
KMONS: P = plant
MONS:  apis, spriggan ; robe . quarterstaff
ITEM:  ration q:3
MAP
ttttttt@t
txxxtt.tttttttt
txd+"""""""tt.@
txxx""""""""".tt
ttt""2"""""""TTt
ttT""11"11"TTTtt
tt""""1""""Tttt
tt".11""2TTtt
tt""1""""Ttt
ttt""""TTTt
  ttttttttt
ENDMAP

NAME:  kb_forest_druid_statue
DEPTH: Forest
MONS:  spriggan druid
MAP
tt@tt
t.1.t
t1G1t
t.1.t
ttttt
ENDMAP

# XXX: this one doesn't work very well outside the corner, or as a primary
# vault...
NAME:   kb_forest_skeletal_bear_cave
DEPTH:  Forest
ORIENT: southeast
MONS:   greenwood witch
MONS:   black bear skeleton
MAP
                   tttttt
                ttttxxxxx
            t@tttxxxxxxxx
            tt..xxxxxxxxx
          tttt....xxxxxxx
        tttt......}xxxxxx
       tt.....xxxxxxxxxxx
ttt  ttt....xxxxxxxxxxxxx
@.tttt...xxxxxxxxsxxxsxxx
tt.......xxxxx..sxxxxssxx
 tttxxxxx......xx.sxx..xx
txxxxxxxxxx....xxx.1.xxxx
 txxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##############################################################################
# Forest ends
##############################################################################
NAME:   grunt_forest_end_chambers
ORIENT: north
PLACE:  Forest:$
KITEM:  O = superb_item
KITEM:  1 = superb_item
KMONS:  O = the Enchantress / Agnes / spriggan defender, spriggan air mage
KMONS:  1 = spriggan defender
MONS:   spriggan defender, spriggan berserker, spriggan druid
MONS:   spriggan rider, spriggan air mage
MONS:   spriggan defender / spriggan berserker / spriggan druid / \
        spriggan rider / spriggan air mage / nothing w:100
MONS:   thorn hunter / shambling mangrove
TILE:   c = wall_marble
COLOUR: .|*%OT123456+ = lightgray
FTILE:  .|*%OT123456+ = floor_normal
MAP
tttttttttttttttttttttttttttttttttt
tttttttttttttccccccccttttttttttttt
tttttttttttccc||||||cccttttttttttt
ttttttttttcc..........cctttttttttt
tttttttttcc....4444....ccttttttttt
tttttttttc..............cttttttttt
ttttttttcc..6.5....5.6..cctttttttt
ttttttttc%.6..........6.%ctttttttt
ttttttttc*.6..........6.*ctttttttt
ttttttttc*.6....O1....6.*ctttttttt
ttttttttc*.6....11....6.*ctttttttt
ttttttttc*.6..........6.*ctttttttt
ttttttttc%.6..5....5..6.%ctttttttt
ttttttttcc..6........6..cctttttttt
tttttttttc..............cttttttttt
tttttttttcc....4224....ccttttttttt
ttttcccccccc..........cccccccctttt
tttcc......ccc......ccc......ccttt
ttcc.4.4.4..cccc++cccc..4.4.4.cctt
tcc..........c.6..6.c..........cct
tc%..........c+.66.+c..........%ct
tc%......1....cccccc....1......%ct
tc%....T.2....cccccc....2.T....%ct
tc%...........cccccc...........%ct
tc%.6........c+.66.+c........6.%ct
tcc.6........c.6..6.c........6.cct
ttc..66.....cccc++cccc.....66..ctt
ttccc..66..cccc....cccc..66..ccctt
ttttccc..cccc........cccc..cccLttt
tLttLLcccccc...6446...ccccccLtLtLt
ttLLLLtLLLcc..........cctLLLtttLLt
ttLLLLLLLLc..6......6..cLLtLLtttLt
tLLtLLL7LLc..6..TT..6..cLLLLtLLLLt
ttLLLLLLLLc..6..TT..6..cLLLLLLLLLt
tttLtLLLLLc..6......6..cLLtLLLtLLt
ttLLLLLtLLcc..........ccLLL7LLLLLt
ttLL7LLLLLLc...6666...cLLLLLtLL7Lt
tLLLLLLLLLLcc........ccLLtLLLLLLtt
ttLLtLL7LLLLccc....cccLL7LLLtLtttt
tttttLLLLLLLLLc++++cLLLLLLtttttttt
tttttttLLLLLLLLLLLLLLLLttLLLtttttt
tttttttttttLLLLLLLLLLtttLLLLLttttt
tttttttttttttttLLLLtttLLLttttttttt
ttttttttttttttttLLLLLLLttttttttttt
ttttttttttttttttL@LLtttttttttttttt
ENDMAP

NAME:   kb_forest_enchantress_palace
ORIENT: northeast
PLACE:  Forest:$
MONS:   spriggan rider / spriggan druid / \
        spriggan berserker / spriggan defender / \
        spriggan air mage
MONS:   spriggan rider
MONS:   spriggan druid
MONS:   spriggan berserker
MONS:   spriggan air mage
MONS:   spriggan defender
KMONS:  O = the Enchantress / Agnes / Jorgrun / spriggan air mage, spriggan air mage
KMONS:  A = greenwood witch
KMONS:  B = will-o'-wisp
KMONS:  X = spriggan rider / spriggan druid / \
            spriggan berserker / spriggan defender / \
            spriggan air mage
KMONS:  Y = spriggan air mage
KFEAT:  ~ = +
SUBST:  . = .:100 / 1:2 / 2:1 / 3:1 / 4:1 / 5:1
SUBST:  T = t / t / P / .
KMONS:  P = plant / plant / bush / nothing
KMONS:  R = plant
KMONS:  Q = bush
KMONS:  K = hornet
KITEM:  f = dagger / short sword / rapier / flail / whip /  shortbow
KITEM:  g = robe / cloak / hat / buckler
KMONS:  L = spriggan ; robe . dagger
KITEM:  h = ration q:2
COLOUR: C = white
KFEAT:  C = stone_wall
TILE:   C = wall_marble
KFEAT:  - = floor
FTILE:  R-~67ABXYfg$*| = floor_normal
MAP
ttttttttttttttttttttttttttttttttttttttttttt
tttCCCCCCCCCCCCCCtttttttttttttttttttttttttt
tttC-----O------CtttttttttTTTTTttTTTTtttttt
tttC--B------A--CtttQTtttTT.T.Ttt.T.TTTtttt
tttC----6--6----Ctt..ccccc....cccc.TT.Ttttt
tttCCCCCC~~CCCCCC....c..1c....c.1c.t.TTtttt
tttC---C6--6C$*|CtT..c.1.+....+..cT.TT.tttt
tttC---~----~$*|Ctt..ccccc....cccc.TT.Ttttt
tttC---C6--6C$*|CttQ..................ttttt
tttCCCCCC~~CCCCCCttttccccc....cccc......ttt
tttCgggC--X-CfffCttttchxLc....c1.c.....tttt
tttCggg~----~fffCttttch..+....+.4c..1...ttt
tttCgggCX--XCfffCttttccccc....cccc...1.tttt
tttCCCCCC~~CCCCCCttttt...................tt
tttCR-----X----RCccccccccc....5.........ttt
tttC--X--------XCcKxKxKxKc......2......tttt
tttCRX--Y--Y--XRCc.22.3..c.....22......tttt
tttCCCCCC~~CCCCCCc+ccccccc....2.2.....ttttt
ttt..................................tttttt
@...................................ttttttt
t..TTttttt............5..........ttt.tttttt
tttttttttttttttt...............tttttt...ttt
ttttttttttttttttttt.........tttttttttttt<tt
ttttttttttttttttttttttt....tttttttttttttttt
tttttttttttttttttttttt@tt..@ttttttttttttttt
ENDMAP

NAME:   grunt_forest_end_winery
TAGS:   no_pool_fixup
PLACE:  Forest:$
ORIENT: northeast
KMONS:  O = the Enchantress band, Frances, spriggan defender
MONS:   spriggan, spriggan berserker / spriggan defender
MONS:   spriggan druid / spriggan air mage, plant
MONS:   faun w:20 / satyr / thorn hunter w:5
KMONS:  S = spark wasp
KMONS:  K = caustic shrike
NSUBST: 6 = 1:K / 4:S / *:d
KMONS:  7 = water nymph
KFEAT:  7 = deep_water
NSUBST: w = 2:7 / *:w
KFEAT:  T = tree / floor
SUBST:  X = xt
CLEAR:  x
KFEAT:  : = floor
ITEM:   ration q:1 / ration q:2 / nothing w:180
ITEM:   any potion / ration q:1 / nothing w:30
TILE:   c = wall_vines
FTILE:  e123+|%*$- = floor_vines
MAP
tttttttttttttttttttttttttttttttttttttttttttt
Xttttttttttttttttttttttttttttttttttttttttttt
XttTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTtt
XttT.5...................................Ttt
XttT..4d4d4.cccccccccccccccccccccccccccc.Ttt
XttT..4d4d4.ce-e-e-e-e-e-e-eceeeeee+|||c.Ttt
XttT..4d4d4.c-e-e-e-c-e-e-e-c------c|||c.Ttt
XttT..4d4d4.ce-e2e-ece-e2e-ec------cc+cc.Ttt
XttT..4d4d4.cc+ccccccc+cccccc------c%**c.Ttt
XttT..4d4d45c-----3---3-----+----O-c%**c.Ttt
XttT..4d4d4.c-------2-------+------c%**c.Ttt
XtT...4d4d4.c--wwwwwwwwwww--+------c%**c.Ttt
XtT...4d4d4.c--wwwwwwwwwww--c------c$$$c.Ttt
XtT...4d4d4.c-----3---3-----c------c$$$c.Ttt
XtT...4d4d4.c---------------ceeeeee+$$$c.Ttt
XTT...4d4d4.cc++cccccccccccccccccccccccc.Ttt
T.......5......5............5............Ttt
@TT...44444444..444444444444..4444444444.Ttt
TtT...ddd6dd6d..ddd6ddd6dddd..ddd6ddd6dd.Ttt
XtT...444444445.444444444444.54444444444.Ttt
XtT...d6dddd6d..d6ddd6dd6ddd..d6dddd6ddd.Ttt
XttT..44444444.54444444444445.4444444444.Ttt
XttT..dd6ddd6d..dddd6dddd6dd..d6ddd6dddd.Ttt
XttT..44444444..444444444444..4444444444.Ttt
XttT.....................................Ttt
XttT.....................................Ttt
XttTTTTTT...........TTTT...........TTTTTTTtt
XttttttttTTTTT.TTTTTTTTTTTTTT.TTTTTTTTTTTTtt
XXtttttttttttT.TttttttttttttT.Tttttttttttttt
XXXXXXXXXXXXT@TXXXXXXXXXXXXXXT@TXXXXXXXXXXXt
ENDMAP

NAME:   grunt_forest_end_shrine
PLACE:  Forest:$
ORIENT: float
KMONS:  O = the Enchantress / Bai Suzhen, spriggan air mage
MONS:   spriggan, wendigo / shambling mangrove
MONS:   spriggan air mage / spriggan druid / \
        spriggan berserker / spriggan rider w:5 / \
        spriggan defender w:5 / \ 
        thorn hunter w:5 / shambling mangrove w:5
MONS:   will-o'-wisp
KFEAT:  _ = altar_xom
KFEAT:  - = floor
SUBST:  T = tx
CLEAR:  x
FTILE:  14|*$-+ = floor_dirt
TILE:   c = wall_church
MAP
  TTTTTTTTTTTTT
 TTtttttttttttTT
TTtt.........ttTT
Ttt3.........3ttT
Tt..ccccccccc..tTTTTTTTT
Tt..c||c4$$$c..ttttttttt
Tt..c|-c****c.2t..3....t
Tt..cc+c-2-2+....G.G.G..
Tt2.c-2--O_-+..2.......@
Tt..cc+c-2-2+....G.G.G..
Tt..c|-c****c.2t..3....t
Tt..c||c4$$$c..ttttttttt
Tt..ccccccccc..tTTTTTTTT
Ttt3.........3ttT
TTtt.........ttTT
 TTtttttttttttTT
  TTTTTTTTTTTTT
ENDMAP

## A camper cannibalizes his companions to survive and turns into a wendigo
NAME:  manman_forest_wendigo_camp
DEPTH: Forest
MONS:  wendigo
: dgn.delayed_decay(_G, 'd', 'human skeleton ')
MARKER: L = lua:fog_machine { cloud_type = "flame", \
             pow_min = 1000, pow_max = 1000, delay = 1, \
             size = 1, walk_dist = 0, start_clouds = 1, excl_rad = 0 }
MAP
        tttttt
      tttttttttt
    tttttt.dtttttt
  ttttttt....ttttttt
 tttttttd.....ttttttt
ttttttt...LL1..tttttt
ttttttt...LLd..tttttt
 ttttttt......ttttt
  tttttttd..ttttt
    tttttt.tttttt
      tttt.ttttt
       ttt.ttt
        ttdtt
        tt@tt
ENDMAP

###############################################################################
## A palace where denizens of the forest gather to worship a powerful
## water deity
NAME:     gammafunk_forest_water_palace
PLACE:    Forest:$
ORIENT:   north
TAGS:     no_monster_gen no_rotate patrolling
SHUFFLE:  HI / JK, QR / OP
SUBVAULT: H : gammafunk_forest_water_palace_column
SUBVAULT: I : gammafunk_forest_water_palace_column
SUBVAULT: J : gammafunk_forest_water_palace_pool
SUBVAULT: K : gammafunk_forest_water_palace_pool
SUBVAULT: Q : gammafunk_forest_water_palace_column
SUBVAULT: R : gammafunk_forest_water_palace_column
SUBVAULT: O : gammafunk_forest_water_palace_pool
SUBVAULT: P : gammafunk_forest_water_palace_pool
MONS:     nothing w:30 / spriggan air mage / spriggan druid 
MONS:     w:20 faun / satyr, spriggan berserker
KMONS:    4 = caustic shrike
KMONS:    5 = spriggan defender
KMONS:    6 = shambling mangrove / thorn hunter
KMONS:    7 = black bear
KMONS:    8 = the Enchantress / Agnes / spriggan air mage, spriggan air mage
KMONS:    A = water elemental
KMONS:    L = greenwood witch / wendigo w:30
KMONS:    N = water nymph
KMONS:    S = elemental wellspring
KFEAT:    ALNS = shallow_water
COLOUR:   c = white
FTILE:    |*123456GTU+. = floor_pebble_cyan
CLEAR:    E
MAP
ttttttttttttttttttttttttttttttttttttttttttttttttt
Ett.....WWWWWWWWcccccccccccccccccWWWWWWWW.....ttE
Ett....WWWWWWWWcc*..|||||||||..*ccWWWWWWWW....ttE
Ett....WWWGLWWWc**.....585.....**cWWWLGWWW....ttE
Ett....WWWWWWWWc**.4..........4**cWWWWWWWW....ttE
Ett....WWWWWccccccccccc+++cccccccccccWWWWW....ttE
Ett....WWWWccWWWWWWWWWWWWWWWWWWWWWWWccWWWW....ttE
Ett.1..WWWWcWWWWWWWWWWWWWWWWWWWWWWWWWcWWWW..1.ttE
Ett....WWLWcWWHHHWWSWWW.U.WWWSWWIIIWWcWWLW....ttE
Ett....WWWWcWWHHHWWWWWU...UWWWWWIIIWWcWWWW....ttE
Ett....WWWWcWWHHHWWWW.......WWWWIIIWWcWWWW....ttE
Ett.1..WWWWcWWWWWWWWW..555..WWWWWWWWWcWWWW..1.ttE
Ett....WWWWcWWWWWWWWW.......WWWWWWWWWcWWWW....ttE
Ett....WWWWcWWWQQQWWWW.....WWWWRRRWWWcWWWW....ttE
Ett....WWWWcWWWQQQWWWWW...WWWWWRRRWWWcWWWW....ttE
Ett.1..WWLWcWWWQQQWWLWWWWWWWLWWRRRWWWcWWLW..1.ttE
Ett....WWWWcWWWWWWWWWWWWWWWWWWWWWWWWWcWWWW....ttE
Ett....WWWWcWWWWWWWWNWWWWWWWNWWWWWWWWcWWWW....ttE
Ett....WWWWcWWWWWWWWWWWWWWWWWWWWWWWWWcWWWW....ttE
Ett.1..WWWWcWWWcccWWWWW...WWWWWcccWWWcWWWW..1.ttE
Ett....WWWWcWWWcTcWWWW.2.3.WWWWcTcWWWcWWWW....ttE
Ett....WWWWcWWW.7.WWWW.....WWWW.7.WWWcWWWW....ttE
Ett....WWWWcWWWWWWWWWW.3.2.WWWWWWWWWWcWWWW....ttE
Ett....WWLWcWWWWLWWWWW.....WWWWWLWWWWcWWLW....ttE
Ett.1..WWWWccWWWWWWWWW.....WWWWWWWWWccWWWW..1.ttE
Ett....WWWWWccccccccccc+++cccccccccccWWWWW....ttE
Ett....WWWWWWWWc.................cWWWWWWWW....ttE
Ett....WWWWWWWWc..T...2...3...T..cWWWWWWWW....ttE
Ett....WWcccWWWc......3...2......cWWWcccWW....ttE
Ett....WWcGcWWWc...T..2...3..T...cWWWcGcWW....ttE
Ett....WWWWWWWWcc...............ccWWWWWWWW....ttE
Ett.....WWWWNWWWccccccc+++cccccccWWWNWWWW.....ttE
Ettt......WWWWWWWAWWAWWWWWWAWWAWWWWWWWW......tttE
Etttt......WWWWWWWWWWWWWWWWWWWWWWWWW........ttttE
Etttttt............T.........T............ttttttE
Etttttttt.....6...................6.....ttttttttE
Etttttttttt........T.........T........ttttttttttE
Ettttttttttttt.....................tttttttttttttE
Ettttttttttttttttt.............tttttttttttttttttE
Etttttttttttttttttttt.......ttttttttttttttttttttE
EEEEEEEEEEEEEEEEEEEEEEE.@.EEEEEEEEEEEEEEEEEEEEEEE
ENDMAP

NAME: gammafunk_forest_column
TAGS: gammafunk_forest_water_palace_column unrand no_rotate no_flip no_vmirror \
      allow_dup patrolling no_monster_gen
KMONS:  4 = black bear
COLOUR: c = white
FTILE:  G4. = floor_pebble_cyan
MAP
ccc
cGc
.4.
ENDMAP

NAME:  gammafunk_forest_pool
TAGS:  gammafunk_forest_water_palace_pool unrand no_rotate no_vmirror
TAGS:  allow_dup patrolling no_monster_gen
KMONS: N = water nymph / will-o'-wisp
KFEAT: NL = shallow_water
MAP
WWW
WNW
WWW
ENDMAP
